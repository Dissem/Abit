apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'idea'

ext {
    appName = "Abit"
}
if (project.hasProperty("project.configs")
    && new File(project.property("project.configs") + appName + ".gradle").exists()) {
    apply from: project.property("project.configs") + appName + ".gradle"
}

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "ch.dissem.apps." + appName.toLowerCase()
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 17
        versionName "1.0-beta17"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/core.kotlin_module'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

//ext.jabitVersion = '2.0.4'
ext.jabitVersion = 'feature-refactoring-SNAPSHOT'
ext.supportVersion = '27.0.1'
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"

    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:preference-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:multidex:1.0.2"

    implementation "ch.dissem.jabit:jabit-core:$jabitVersion"
    implementation "ch.dissem.jabit:jabit-networking:$jabitVersion"
    implementation "ch.dissem.jabit:jabit-cryptography-spongy:$jabitVersion"
    implementation "ch.dissem.jabit:jabit-extensions:$jabitVersion"
    implementation "ch.dissem.jabit:jabit-wif:$jabitVersion"
    implementation "ch.dissem.jabit:jabit-exports:$jabitVersion"

    implementation 'org.slf4j:slf4j-android:1.7.25'

    implementation 'com.mikepenz:materialize:1.1.0@aar'
    implementation('com.mikepenz:materialdrawer:5.9.5@aar') {
        transitive = true
    }
    implementation('com.mikepenz:aboutlibraries:5.9.7@aar') {
        transitive = true
    }
    implementation "com.mikepenz:iconics-core:2.9.3@aar"
    implementation "com.mikepenz:iconics-views:2.9.3@aar"
    implementation 'com.mikepenz:google-material-typeface:3.0.1.1.original@aar'
    implementation 'com.mikepenz:community-material-typeface:1.9.32.2@aar'

    implementation 'com.journeyapps:zxing-android-embedded:3.5.0@aar'
    implementation 'com.google.zxing:core:3.3.1'

    implementation 'com.github.kobakei:MaterialFabSpeedDial:1.1.7'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation('com.github.h6ah4i:android-advancedrecyclerview:0.11.0@aar') {
        transitive = true
    }
    implementation 'com.github.angads25:filepicker:1.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.11.0'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'org.robolectric:robolectric:3.5'
    testImplementation "org.robolectric:shadows-multidex:3.5"

    androidTestImplementation "com.android.support:multidex:1.0.2"
}

idea.module {
    downloadJavadoc = true
    downloadSources = true
}
